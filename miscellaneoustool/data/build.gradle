plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.hilt)
    alias(libs.plugins.protobuf)
    alias(libs.plugins.kotlin.kapt)
}

android {
    namespace 'com.jinproject.data'
    compileSdk 33

    defaultConfig {
        minSdkVersion 26

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        unitTests.all {
            it.useJUnitPlatform()
        }
    }
    sourceSets {
        getByName("androidTest").assets.srcDir("$projectDir/schemas")
    }
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":core"))
    testImplementation (libs.JUnit)
    androidTestImplementation (libs.test.ext)
    androidTestImplementation (libs.test.espresso)

    kapt(libs.dagger.hilt.compiler)
    implementation(libs.bundles.hilt)

    implementation(libs.coroutines)
    implementation(libs.bundles.datastore)

    implementation(libs.room.ktx)
    kapt(libs.room.compiler)
    androidTestImplementation(libs.room.testing)
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.18.0"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}